apply plugin: 'com.android.application'

def keyStorePropertiesFile = rootProject.file('local.static.properties')
def keyStoreProperties = new Properties()
keyStoreProperties.load(new FileInputStream(keyStorePropertiesFile))

def projectPropertiesFile = rootProject.file('project.properties')
def projectProperties = new Properties()
projectProperties.load(new FileInputStream(projectPropertiesFile))

android {
    compileSdkVersion = 29
    buildToolsVersion = '29.0.2'
    def appIdParts = projectProperties['applicationId'].split(/\./)
    def nameCount = appIdParts.size()
    def appIdLastPart = appIdParts[nameCount-1]

    assetPacks = [":game"]

    defaultConfig {
        applicationId projectProperties['applicationId']
        archivesBaseName = "app-" + appIdLastPart
        minSdkVersion 19
        targetSdkVersion 29
        versionCode Integer.parseInt(projectProperties.getProperty('versionCode'))
        versionName projectProperties['versionName']
    }

    signingConfigs {
        release {
            storeFile file(keyStoreProperties['storeFile'])
            storePassword keyStoreProperties['storePassword']
            keyAlias keyStoreProperties['keyAlias']
            keyPassword keyStoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    api project(':runtime')
    implementation 'androidx.core:core:1.3.2'
}
